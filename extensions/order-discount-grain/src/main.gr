import File from "sys/file"
import Result from "result"
import Option from "option"
import String from "string"
import Number from "number"
import List from "list"
import Json from "./json"

let log = (logStr: String) => {
    File.fdWrite(File.stderr, logStr ++ "\n")
}

let rec readInput = (input: String) => {
    let (partialInput, bytesRead) = Result.unwrap(File.fdRead(File.stdin, 1024))
    let mut result = String.concat(input, partialInput)
    if (bytesRead == 1024) {
        readInput(result)
    } else {
        result
    }    
}

let inputStr = readInput("")
let input = Result.toOption(Json.parse(inputStr))

let jsonProperty = (name, json) => {
    match (json) {
        Json.JSONObject(list) => {
            let entry = List.find((entryTest) => {
                let (entryTestName, _) = entryTest
                entryTestName == name
            }, list)
            Option.map(entry => {
                let (_, entryJson) = entry
                entryJson
            }, entry)
        },
        _ => {
            fail("Unexpected input")
        }
    }
}

/* A little custom operator magic from Grain's @ospencer to clean up JSON navigation. */
let property = field => json => jsonProperty(field, json)
let (|>) = (option, transform) => Option.flatMap(transform, option)

record Configuration { discountPercentage: Number, qualifyingProductTotal: Number }

let getConfiguration = (json) => {
    let valueJson = json |> property("discountNode") |> property("metafield") |> property("value")

    let value = match(valueJson) {
        Some(valueJson) => match (valueJson) {
            Json.JSONString(string) => string,
            _ => "{}"
        },
        None => "{}"
    }

    let configurationJson = Result.toOption(Json.parse(value))
    let percentageJson = configurationJson |> property("discountPercentage")
    let qualifyingTotalJson = configurationJson |> property("qualifyingProductTotal")
    {
        discountPercentage: match (percentageJson) {
            Some(percentageJson) => match (percentageJson) {
                Json.JSONNumber(number) => number,
                _ => 0
            },
            None => 0
        },
        qualifyingProductTotal: match (qualifyingTotalJson) {
            Some(qualifyingTotalJson) => match (qualifyingTotalJson) {
                Json.JSONNumber(number) => number,
                _ => 0
            },
            None => 0
        }
    }

}

let isVip = (json) => {   
    let valueJson = json |> property("cart") |> property("buyerIdentity") |> property("customer") |> property("metafield") |> property("value")
    match (valueJson) {
        Some(value) => match (value) {
            Json.JSONString(string) => string == "true",
            _ => false
        },
        None => false
    }
}

let getQualifyingTotal = (linesJsonArray) => {
    let qualifyingLines = List.filter((line => {
        let isQualifyingJson = Some(line) |> property("merchandise") |> property("product") |> property("isQualifying")
        match (isQualifyingJson) {
            Some(value) => match (value) {
                Json.JSONBoolean(boolean) => boolean,
                _ => false
            },
            None => false
        }
    }), linesJsonArray)
    List.reduce((accum, line) => {
        let lineTotalJson = Some(line) |> property("cost") |> property("totalAmount") |> property("amount")
        match (lineTotalJson) {
            Some(value) => match (value) {
                Json.JSONString(numberStr) => Result.unwrap(Number.parseFloat(numberStr)),
                _ => 0
            },
            None => 0
        }
    }, 0, qualifyingLines)
}

let cartQualifies = (json, configuration) => {
    let linesJson = json |> property("cart") |> property("lines")
    match (linesJson) {
        Some(value) => match (value) {
            Json.JSONArray(array) => {
                let qualifyingTotal = getQualifyingTotal(array)
                qualifyingTotal >= configuration.qualifyingProductTotal
            },
            _ => false
        },
        None => false
    }
}

let discountFunction = (json) => {
    let noDiscount = "{
        \"discounts\": [],
        \"discountApplicationStrategy\": \"FIRST\"
    }"

    let configuration = getConfiguration(json);

    if (!isVip(json)) {
        log("Customer is not a VIP")
        noDiscount
    } else if (!cartQualifies(json, configuration)) {
        log("Qualifying cart item total is unmet")
        noDiscount
    } else {
        "{
            \"discounts\": [
                {
                    \"value\": {
                        \"percentage\": {
                            \"value\":" ++ toString(configuration.discountPercentage) ++ "
                        }
                    },
                    \"targets\": [
                        {
                            \"orderSubtotal\": {
                                \"excludedVariantIds\": []
                            }
                        }
                    ],
                    \"message\": \"VIP Discount\"
                }
            ],
            \"discountApplicationStrategy\": \"FIRST\"
        }"
    }
}

let outputJson = discountFunction(input)
print(outputJson)
